USE [DHTN-2025]
GO
/****** Object:  StoredProcedure [edoc].[Prc_IncomingDoc_UpdateProcessMultiId]    Script Date: 9/15/2025 3:04:51 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:      Tran Duc Binh
-- Create date: 14/09/2025
-- Description: Update process multi incoming document by updating status, log opinion
-- =============================================
ALTER PROCEDURE [edoc].[Prc_IncomingDoc_UpdateProcessMultiId]
    @UserId INT,
    @RoleId INT,
    @Status INT = NULL,
    @Opinion NVARCHAR(MAX),
    @OpinionType INT,
    @ReturnStatus BIT = NULL,
    @IncomingDocIds XML
AS
BEGIN
    BEGIN TRY
    BEGIN TRANSACTION;
        --
        -- All other declarations and initialisation
        --
        DECLARE @current_date DATETIME = GETDATE();
        DECLARE @IncomingDocId BIGINT;

        -- Create temporary table to store IncomingDocIds
        IF OBJECT_ID('tempdb..#IncomingDocIds') IS NOT NULL
            DROP TABLE #IncomingDocIds;
        CREATE TABLE #IncomingDocIds (IncomingDocId BIGINT);

        -- Parse IncomingDocIds XML
        INSERT INTO #IncomingDocIds (IncomingDocId)
        SELECT x.v.value('.', 'BIGINT')
        FROM @IncomingDocIds.nodes('IncomingDocIds/IncomingDocId') x(v);

        -- Declare cursor to iterate over IncomingDocIds
        DECLARE doc_cursor CURSOR FOR
        SELECT IncomingDocId
        FROM #IncomingDocIds;

        OPEN doc_cursor;
        FETCH NEXT FROM doc_cursor INTO @IncomingDocId;

        WHILE @@FETCH_STATUS = 0
        BEGIN
            -- Update logic
            IF @ReturnStatus IS NOT NULL
			BEGIN
				-- Update the IncomingDoc ReturnStatus
				UPDATE edoc.IncomingDoc
				SET ReturnStatus = @ReturnStatus,
					ModifiedUserId = @UserId,
					ModifiedDate = @current_date
				WHERE Id = @IncomingDocId;
			END
		
		IF @Status IS NOT NULL
			BEGIN
				-- Update the IncomingDoc status
				UPDATE edoc.IncomingDoc
				SET Status = @Status,
					ModifiedUserId = @UserId,
					ModifiedDate = @current_date
				WHERE Id = @IncomingDocId;
			END

            -- Insert opinion into OpinionIncomingDoc
            INSERT INTO edoc.OpinionIncomingDoc (
                IncomingDocId,
                OpinionUserId,
                Opinion,
                Status,
                OpinionDate,
                OpinionType,
                OpinionUserRoleId
            )
            VALUES (
                @IncomingDocId,
                @UserId,
                @Opinion,
                1,
                @current_date,
                @OpinionType,
                @RoleId
            );

            FETCH NEXT FROM doc_cursor INTO @IncomingDocId;
        END;

        -- Clean up cursor
        CLOSE doc_cursor;
        DEALLOCATE doc_cursor;

        -- Clean up temporary table
        DROP TABLE #IncomingDocIds;

    COMMIT TRANSACTION;
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION;
        DECLARE @ErrorMessage NVARCHAR(MAX),
                @ErrorNumber INT,
                @ErrorSeverity INT,
                @ErrorState INT,
                @ErrorLine INT,
                @ErrorProcedure NVARCHAR(200);

        -- Assign variables to error-handling functions that capture information for RAISERROR.
        SELECT @ErrorNumber = ERROR_NUMBER(),
               @ErrorSeverity = ERROR_SEVERITY(),
               @ErrorState = ERROR_STATE(),
               @ErrorLine = ERROR_LINE(),
               @ErrorProcedure = ISNULL(ERROR_PROCEDURE(), '-');

        -- Build the message string that will contain original error information.
        SELECT @ErrorMessage = N'Error %d, Level %d, State %d, Procedure %s, Line %d, Message: ' + ERROR_MESSAGE();

        -- Only set the error state if its been set to zero
        IF (@ErrorState = 0)
            SET @ErrorState = 1;

        -- Raise an error: msg_str parameter of RAISERROR will contain the original error information.
        RAISERROR(   @ErrorMessage,
                     @ErrorSeverity,
                     @ErrorState,
                     @ErrorNumber,    -- parameter: original error number.
                     @ErrorSeverity,  -- parameter: original error severity.
                     @ErrorState,     -- parameter: original error state.
                     @ErrorProcedure, -- parameter: original error procedure name.
                     @ErrorLine       -- parameter: original error line number.
                 );
    END CATCH;
END;
