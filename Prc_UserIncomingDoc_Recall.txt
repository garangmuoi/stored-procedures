USE [DHTN-2025]
GO
/****** Object:  StoredProcedure [edoc].[Prc_UserIncomingDoc_Recall]    Script Date: 12/9/2025 2:31:52 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:      Tran Duc Binh
-- Create date: 14/09/2025
-- Description: Thu hồi văn bản
-- =============================================
ALTER PROCEDURE [edoc].[Prc_UserIncomingDoc_Recall]
    @UserId INT,
    @RoleId INT,
    @UserIncomingDocs XML = NULL,
    @IncomingDocId BIGINT = NULL,
    @Opinion NVARCHAR(MAX)
AS
BEGIN
    BEGIN TRY
    BEGIN TRANSACTION;
        --
        -- All other declarations and initialisation
        --
        DECLARE @current_date DATETIME = GETDATE(),
                @OpinionIncomingDocId INT,
                @RoleCode NVARCHAR(100),
                @UserIncomingDocId BIGINT,
                @UserSendStatus INT = NULL;

        SELECT @RoleCode = Code
        FROM dbo.Role
        WHERE Id = @RoleId

        -- Có @UserIncomingDocs
        IF @UserIncomingDocs IS NOT NULL
        BEGIN
            -- Declare temp table to parse Recipients
		    IF OBJECT_ID('tempdb..#UserIncomingDocs') IS NOT NULL
			    DROP TABLE UserIncomingDocs;
		    
            CREATE TABLE #UserIncomingDocs
		    (
			    UserIncomingDocId BIGINT,
			    IncomingDocId BIGINT,
                UserSendStatus INT
		    );

		    -- Parse the XML into the temp table UserIncomingDocs
		    INSERT INTO #UserIncomingDocs (UserIncomingDocId, IncomingDocId, UserSendStatus)
		    SELECT 
			    x.v.value('(UserIncomingDocId)[1]', 'INT') AS UserIncomingDocId,
			    x.v.value('(IncomingDocId)[1]', 'INT') AS IncomingDocId,
                u.UserSendStatus
		    FROM @UserIncomingDocs.nodes('/UserIncomingDocs/UserIncomingDoc') x(v)
            LEFT JOIN edoc.UserIncomingDoc u ON u.Id = x.v.value('(UserIncomingDocId)[1]', 'BIGINT') and u.UserSendStatus IS NOT NULL;

            -- Tập các UserIncomingDocId bị ảnh hưởng (bao gồm bản chính + những bản có UserSendStatus > bản chính)
            IF OBJECT_ID('tempdb..#UserIncomingAffected') IS NOT NULL
                DROP TABLE #UserIncomingAffected;

            CREATE TABLE #UserIncomingAffected
            (
                UserIncomingDocId BIGINT
            );

		    -- Cursor to iterate over Recipients
		    DECLARE incoming_cursor CURSOR FOR
			    SELECT UserIncomingDocId, IncomingDocId, UserSendStatus
			    FROM #UserIncomingDocs
			    WHERE UserIncomingDocId IS NOT NULL;

		    OPEN incoming_cursor;
		    FETCH NEXT FROM incoming_cursor 
		    INTO @UserIncomingDocId, @IncomingDocId, @UserSendStatus;

		    WHILE @@FETCH_STATUS = 0
		    BEGIN

                -- Lấy danh sách UserIncomingDocId liên quan
                INSERT INTO #UserIncomingAffected (UserIncomingDocId)
                SELECT Id FROM edoc.UserIncomingDoc
                WHERE Id = @UserIncomingDocId 
                    or Id IN (SELECT Id FROM edoc.UserIncomingDoc as u WHERE u.IncomingDocId = @IncomingDocId and u.UserSendStatus IS NOT NULL and u.UserSendStatus > @UserSendStatus);

                UPDATE u 
                SET
                    Status = 4,  -- Status = 4 Bị Thu Hồi
                    ModifiedDate = @current_date, 
                    ModifiedUserId = @UserId
                FROM edoc.UserIncomingDoc as u
                INNER JOIN #UserIncomingAffected as a ON u.Id = a.UserIncomingDocId;

                UPDATE o
                SET
                    o.Status = 4, -- Status = 4 Bị Thu Hồi
                    o.OpinionDate = @current_date,
                    o.Opinion = @Opinion
                FROM edoc.OpinionIncomingDoc as o
                INNER JOIN #UserIncomingAffected as a
                    ON o.UserIncomingDocId = a.UserIncomingDocId
                   AND o.IncomingDocId = @IncomingDocId
                   AND o.OpinionUserId = @UserId
                   AND o.OpinionUserRoleId = @RoleId
                   AND o.OpinionType = 2;

                INSERT INTO edoc.OpinionIncomingDoc
                (
                    IncomingDocId,
                    UserIncomingDocId,
                    OpinionUserId,
                    OpinionUserRoleId,
                    Opinion,
                    Status,
                    OpinionDate,
                    OpinionType
                )
                SELECT
                    @IncomingDocId,
                    a.UserIncomingDocId,
                    @UserId,
                    @RoleId,
                    @Opinion,
                    4,  -- Bị Thu Hồi
                    @current_date,
                    2   -- Lịch sử
                FROM #UserIncomingAffected as a
                WHERE NOT EXISTS (
                    SELECT 1 FROM edoc.OpinionIncomingDoc as o
                    WHERE o.IncomingDocId = @IncomingDocId
                      AND o.UserIncomingDocId = a.UserIncomingDocId
                      AND o.OpinionUserId = @UserId
                      AND o.OpinionUserRoleId = @RoleId
                      AND o.OpinionType = 2
                );

                IF @RoleCode = 'ThuKyLanhDaoBo'
                    UPDATE edoc.IncomingDoc 
                    SET 
                        Status = 41,  -- Status = 41 Thư ký Lãnh đạo Bộ Thu Hồi
                        ModifiedDate = @current_date, 
                        ModifiedUserId = @UserId
                    WHERE Id = @IncomingDocId;
                ELSE IF @RoleCode = 'VanThuBo'
                    UPDATE edoc.IncomingDoc 
                    SET 
                        Status = 13,  -- Status = 13 Văn thư Bộ Thu Hồi
                        ModifiedDate = @current_date, 
                        ModifiedUserId = @UserId
                    WHERE Id = @IncomingDocId;
                ELSE
                BEGIN
                    -- Khác ThuKyLanhDaoBo
                    IF @UserSendStatus IS NOT NULL
                        UPDATE edoc.IncomingDoc 
                        SET 
                            Status = IIF(@UserSendStatus = 0, 1, @UserSendStatus),
                            ModifiedDate = @current_date, 
                            ModifiedUserId = @UserId 
                        WHERE Id = @IncomingDocId
                    ELSE
                        UPDATE edoc.IncomingDoc 
                        SET 
                            Status = 13, --Status = 13 Văn bản bị thu hồi
                            ModifiedDate = @current_date, 
                            ModifiedUserId = @UserId 
                        WHERE Id = @IncomingDocId
                END

                FETCH NEXT FROM incoming_cursor INTO @UserIncomingDocId, @IncomingDocId, @UserSendStatus;
		    END
		
            -- Clean up cursor
            CLOSE incoming_cursor;
		    DEALLOCATE incoming_cursor;
        END
        ELSE
        BEGIN
            -- Không có @UserIncomingDocs
            IF @IncomingDocId IS NOT NULL
            BEGIN
                IF @RoleCode = 'VanThuDonVi' OR @RoleCode = 'LanhDaoHanhChinh'
                    UPDATE edoc.IncomingDoc 
                    SET 
                        Status = IIF(@RoleCode = 'VanThuDonVi', 1, 5),
                        ModifiedDate = @current_date, 
                        ModifiedUserId = @UserId 
                    WHERE Id = @IncomingDocId

                UPDATE edoc.UserIncomingDoc
                SET 
                    Status = 4, -- Status = 4 Bị Thu Hồi
                    ModifiedDate = @current_date,
                    ModifiedUserId = @UserId
                WHERE IncomingDocId = @IncomingDocId

                DECLARE cur_loop CURSOR FOR
                    SELECT Id 
                    FROM edoc.UserIncomingDoc
                    WHERE IncomingDocId = @IncomingDocId 
                      AND Status = 4;

                OPEN cur_loop;
                FETCH NEXT FROM cur_loop INTO @UserIncomingDocId;

                WHILE @@FETCH_STATUS = 0
                BEGIN
                    SET @OpinionIncomingDocId = NULL;
                    
                    SELECT TOP 1 @OpinionIncomingDocId = o.Id 
                    FROM edoc.OpinionIncomingDoc AS o 
                    WHERE o.IncomingDocId = @IncomingDocId 
                        AND o.UserIncomingDocId = @UserIncomingDocId
                        AND o.OpinionUserId = @UserId
                        AND o.OpinionUserRoleId = @RoleId
                        AND o.Status = 4
                        AND o.OpinionType = 2;

                    IF (@OpinionIncomingDocId IS NULL)
                    BEGIN
                        INSERT INTO edoc.OpinionIncomingDoc
                        (
                            IncomingDocId,
                            UserIncomingDocId,
                            OpinionUserId,
                            OpinionUserRoleId,
                            Opinion,
                            Status,
                            OpinionDate,
                            OpinionType
                        )
                        VALUES
                        (
                            @IncomingDocId, 
                            @UserIncomingDocId, 
                            @UserId, 
                            @RoleId, 
                            @Opinion, 
                            4,  -- Bị Thu Hồi
                            @current_date, 
                            2   -- Lịch sử
                        );
                    END
                    ELSE
                    BEGIN
                        UPDATE edoc.OpinionIncomingDoc 
                        SET 
                            Status = 4, 
                            OpinionDate = @current_date, 
                            Opinion = @Opinion 
                        WHERE Id = @OpinionIncomingDocId
                    END

                    FETCH NEXT FROM cur_loop INTO @UserIncomingDocId;
                END

                CLOSE cur_loop;
                DEALLOCATE cur_loop;
            END
        END

    COMMIT TRANSACTION;
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION;
        DECLARE @ErrorMessage NVARCHAR(MAX),
                @ErrorNumber INT,
                @ErrorSeverity INT,
                @ErrorState INT,
                @ErrorLine INT,
                @ErrorProcedure NVARCHAR(200);

        -- Assign variables to error-handling functions that capture information for RAISERROR.
        SELECT @ErrorNumber = ERROR_NUMBER(),
               @ErrorSeverity = ERROR_SEVERITY(),
               @ErrorState = ERROR_STATE(),
               @ErrorLine = ERROR_LINE(),
               @ErrorProcedure = ISNULL(ERROR_PROCEDURE(), '-');

        -- Build the message string that will contain original error information.
        SELECT @ErrorMessage = N'Error %d, Level %d, State %d, Procedure %s, Line %d, Message: ' + ERROR_MESSAGE();

        -- Only set the error state if its been set to zero
        IF (@ErrorState = 0)
            SET @ErrorState = 1;

        -- Raise an error: msg_str parameter of RAISERROR will contain the original error information.
        RAISERROR(   @ErrorMessage,
                     @ErrorSeverity,
                     @ErrorState,
                     @ErrorNumber,    -- parameter: original error number.
                     @ErrorSeverity,  -- parameter: original error severity.
                     @ErrorState,     -- parameter: original error state.
                     @ErrorProcedure, -- parameter: original error procedure name.
                     @ErrorLine       -- parameter: original error line number.
                 );
    END CATCH;
END;
