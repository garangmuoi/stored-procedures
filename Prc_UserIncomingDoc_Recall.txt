USE [DHTN-2025]
GO
/****** Object:  StoredProcedure [edoc].[Prc_UserIncomingDoc_Recall]    Script Date: 11/13/2025 11:31:10 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:      Tran Duc Binh
-- Create date: 14/09/2025
-- Description: Thu hồi văn bản
-- =============================================
ALTER PROCEDURE [edoc].[Prc_UserIncomingDoc_Recall]
    @UserId INT,
    @RoleId INT,
    @UserIncomingDocs XML = NULL,
    @IncomingDocId BIGINT = NULL,
    @Opinion NVARCHAR(MAX)
AS
BEGIN
    BEGIN TRY
    BEGIN TRANSACTION;
        --
        -- All other declarations and initialisation
        --
        DECLARE @current_date DATETIME = GETDATE(),
                @OpinionIncomingDocId INT,
                @RoleCode NVARCHAR(100),
                @UserIncomingDocId BIGINT,
                @UserSendStatus INT,
                @TotalRecall INT,
                @TotalTransfer INT;

        SELECT @RoleCode = Code
        FROM dbo.Role
        WHERE Id = @RoleId

        IF @UserIncomingDocs IS NOT NULL
        BEGIN
            -- Declare temp table to parse Recipients
		    IF OBJECT_ID('tempdb..#UserIncomingDocs') IS NOT NULL
			    DROP TABLE UserIncomingDocs;
		    CREATE TABLE #UserIncomingDocs
		    (
			    UserIncomingDocId BIGINT,
			    IncomingDocId BIGINT
		    );

		    -- Parse the XML into the temp table UserIncomingDocs
		    INSERT INTO #UserIncomingDocs (UserIncomingDocId, IncomingDocId)
		    SELECT 
			    x.v.value('(UserIncomingDocId)[1]', 'INT') AS UserIncomingDocId,
			    x.v.value('(IncomingDocId)[1]', 'INT') AS IncomingDocId
		    FROM @UserIncomingDocs.nodes('/UserIncomingDocs/UserIncomingDoc') x(v);

		    -- Cursor to iterate over Recipients
		    DECLARE incoming_cursor CURSOR FOR
			    SELECT UserIncomingDocId, IncomingDocId
			    FROM #UserIncomingDocs
			    WHERE UserIncomingDocId IS NOT NULL;

		    OPEN incoming_cursor;
		    FETCH NEXT FROM incoming_cursor 
		    INTO @UserIncomingDocId, @IncomingDocId;

		    WHILE @@FETCH_STATUS = 0
		    BEGIN
                SET @UserSendStatus = NULL;
                SELECT TOP 1 @UserSendStatus = UserSendStatus
                FROM edoc.UserIncomingDoc
                WHERE Id = @UserIncomingDocId;

                UPDATE edoc.UserIncomingDoc
                SET
                    Status = 4,  -- Status = 4 Bị Thu Hồi
                    ModifiedDate = @current_date, 
                    ModifiedUserId = @UserId
                WHERE Id = @UserIncomingDocId;

                SET @OpinionIncomingDocId = NULL;
                SELECT TOP 1 
                    @OpinionIncomingDocId = o.Id 
                FROM edoc.OpinionIncomingDoc as o 
                WHERE o.IncomingDocId = @IncomingDocId 
                    and o.UserIncomingDocId = @UserIncomingDocId
                    and o.OpinionUserId = @UserId
                    and o.OpinionUserRoleId = @RoleId
                    and o.Status = 4
                    and o.OpinionType = 2

                IF (@OpinionIncomingDocId IS NULL)
                BEGIN
                    INSERT INTO edoc.OpinionIncomingDoc
			        (
				        IncomingDocId,
				        UserIncomingDocId,
				        OpinionUserId,
				        OpinionUserRoleId,
				        Opinion,
				        Status,
				        OpinionDate,
				        OpinionType
			        )
			        VALUES
			        (
				        @IncomingDocId, 
				        @UserIncomingDocId, 
				        @UserId, 
				        @RoleId, 
				        @Opinion, 
				        4,  -- Status = 4 Bị Thu Hồi
				        @current_date, 
				        2   -- Lịch sử
			        );
                END
                ELSE
                BEGIN
                    UPDATE edoc.OpinionIncomingDoc 
                    SET 
                        Status = 4,  -- Status = 4 Bị Thu Hồi
                        OpinionDate = @current_date, 
                        Opinion = @Opinion
                    WHERE Id = @OpinionIncomingDocId
                END

                FETCH NEXT FROM incoming_cursor INTO @UserIncomingDocId, @IncomingDocId;
		    END
		
            -- Clean up cursor
            CLOSE incoming_cursor;
		    DEALLOCATE incoming_cursor;

            IF @RoleCode = 'ThuKyLanhDaoBo'
                UPDATE edoc.IncomingDoc 
                SET 
                    Status = 41,  -- Status = 41 Thư ký Lãnh đạo Bộ Thu Hồi
                    ModifiedDate = @current_date, 
                    ModifiedUserId = @UserId
                WHERE Id = @IncomingDocId; -- Status = = 41 Thư ký lãnh đạo bộ Thu hồi
            ELSE
            BEGIN
                -- Khác ThuKyLanhDaoBo
                SELECT @TotalTransfer = COUNT(*) FROM edoc.UserIncomingDoc WHERE IncomingDocId = @IncomingDocId

                SELECT @TotalRecall = COUNT(*) FROM edoc.UserIncomingDoc WHERE IncomingDocId = @IncomingDocId AND Status = 4

                IF @TotalTransfer = @TotalRecall
                    UPDATE edoc.IncomingDoc 
                    SET 
                        Status = 13, -- Status = 13 Thu hồi
                        ModifiedDate = @current_date, 
                        ModifiedUserId = @UserId 
                    WHERE Id = @IncomingDocId
            END
        END
        ELSE
        BEGIN
            -- Không có @UserIncomingDocs
            IF @IncomingDocId IS NOT NULL
            BEGIN
                IF @RoleCode = 'VanThuDonVi' OR @RoleCode = 'LanhDaoHanhChinh'
                    UPDATE edoc.IncomingDoc 
                    SET 
                        Status = 13, -- Status = 13 Thu hồi
                        ModifiedDate = @current_date, 
                        ModifiedUserId = @UserId 
                    WHERE Id = @IncomingDocId

                UPDATE edoc.UserIncomingDoc
                SET 
                    Status = 4, -- Status = 4 Bị Thu Hồi
                    ModifiedDate = @current_date,
                    ModifiedUserId = @UserId
                WHERE IncomingDocId = @IncomingDocId
                --AND ProsessRoleId IN (1,2,3,4)

                DECLARE cur_loop CURSOR FOR
                    SELECT Id 
                    FROM edoc.UserIncomingDoc
                    WHERE IncomingDocId = @IncomingDocId 
                      AND Status = 4;

                OPEN cur_loop;
                FETCH NEXT FROM cur_loop INTO @UserIncomingDocId;

                WHILE @@FETCH_STATUS = 0
                BEGIN
                    SET @OpinionIncomingDocId = NULL;
                    SELECT TOP 1 @OpinionIncomingDocId = o.Id 
                    FROM edoc.OpinionIncomingDoc AS o 
                    WHERE o.IncomingDocId = @IncomingDocId 
                        AND o.UserIncomingDocId = @UserIncomingDocId
                        AND o.OpinionUserId = @UserId
                        AND o.OpinionUserRoleId = @RoleId
                        AND o.Status = 4
                        AND o.OpinionType = 2;

                    IF (@OpinionIncomingDocId IS NULL)
                    BEGIN
                        INSERT INTO edoc.OpinionIncomingDoc
                        (
                            IncomingDocId,
                            UserIncomingDocId,
                            OpinionUserId,
                            OpinionUserRoleId,
                            Opinion,
                            Status,
                            OpinionDate,
                            OpinionType
                        )
                        VALUES
                        (
                            @IncomingDocId, 
                            @UserIncomingDocId, 
                            @UserId, 
                            @RoleId, 
                            @Opinion, 
                            4,  -- Bị Thu Hồi
                            @current_date, 
                            2   -- Lịch sử
                        );
                    END
                    ELSE
                    BEGIN
                        UPDATE edoc.OpinionIncomingDoc 
                        SET 
                            Status = 4, 
                            OpinionDate = @current_date, 
                            Opinion = @Opinion 
                        WHERE Id = @OpinionIncomingDocId
                    END

                    FETCH NEXT FROM cur_loop INTO @UserIncomingDocId;
                END

                CLOSE cur_loop;
                DEALLOCATE cur_loop;
            END
        END

    COMMIT TRANSACTION;
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION;
        DECLARE @ErrorMessage NVARCHAR(MAX),
                @ErrorNumber INT,
                @ErrorSeverity INT,
                @ErrorState INT,
                @ErrorLine INT,
                @ErrorProcedure NVARCHAR(200);

        -- Assign variables to error-handling functions that capture information for RAISERROR.
        SELECT @ErrorNumber = ERROR_NUMBER(),
               @ErrorSeverity = ERROR_SEVERITY(),
               @ErrorState = ERROR_STATE(),
               @ErrorLine = ERROR_LINE(),
               @ErrorProcedure = ISNULL(ERROR_PROCEDURE(), '-');

        -- Build the message string that will contain original error information.
        SELECT @ErrorMessage = N'Error %d, Level %d, State %d, Procedure %s, Line %d, Message: ' + ERROR_MESSAGE();

        -- Only set the error state if its been set to zero
        IF (@ErrorState = 0)
            SET @ErrorState = 1;

        -- Raise an error: msg_str parameter of RAISERROR will contain the original error information.
        RAISERROR(   @ErrorMessage,
                     @ErrorSeverity,
                     @ErrorState,
                     @ErrorNumber,    -- parameter: original error number.
                     @ErrorSeverity,  -- parameter: original error severity.
                     @ErrorState,     -- parameter: original error state.
                     @ErrorProcedure, -- parameter: original error procedure name.
                     @ErrorLine       -- parameter: original error line number.
                 );
    END CATCH;
END;
