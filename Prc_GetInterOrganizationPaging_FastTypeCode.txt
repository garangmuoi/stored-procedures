USE [DHTN-2025]
GO
/****** Object:  StoredProcedure [edoc].[Prc_GetInterOrganizationPaging_FastTypeCode]    Script Date: 11/12/2025 2:30:36 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [edoc].[Prc_GetInterOrganizationPaging_FastTypeCode]
    @SearchText NVARCHAR(250) = NULL,
    @OrgType NVARCHAR(MAX) = NULL,
    @Type INT = NULL -- 1: Chủ trì, 2: Phối hợp
AS
BEGIN
    SET NOCOUNT ON;

    -- Tạo bảng tạm
    CREATE TABLE #OrgTypeTable (OrgType INT);
    CREATE TABLE #SearchIds (Code NVARCHAR(250));

    -- Nếu OrgType có giá trị hợp lệ (khác -1000)
    IF @OrgType IS NOT NULL AND @OrgType <> '-1000'
    BEGIN
        INSERT INTO #OrgTypeTable (OrgType)
        SELECT CAST(value AS INT)
        FROM dbo.SplitString(@OrgType, ',');
    END

    -- Nếu OrgType khác -1000: chia @SearchText thành từng cặp 2 ký tự
    IF @SearchText IS NOT NULL AND @OrgType <> '-1000'
    BEGIN
        IF @Type = 1 -- Chủ trì
        BEGIN
            IF LTRIM(RTRIM(@SearchText)) <> ''
            BEGIN
                INSERT INTO #SearchIds (Code)
                VALUES (LTRIM(RTRIM(@SearchText)));
            END
        END
        
        IF @Type = 2 -- Phối hợp
        BEGIN
            DECLARE @i INT = 1;
            DECLARE @len INT = LEN(@SearchText);
            DECLARE @code NVARCHAR(10);

            WHILE @i <= @len
            BEGIN
                SET @code = SUBSTRING(@SearchText, @i, 2);
                IF LEN(@code) = 2
                    INSERT INTO #SearchIds (Code) VALUES (@code);
                SET @i += 2;
            END
        END
    END

    -- Nếu OrgType = -1000: chia @SearchText theo dấu phẩy
    IF @SearchText IS NOT NULL AND @OrgType = '-1000'
    BEGIN
        INSERT INTO #SearchIds (Code)
        SELECT LTRIM(RTRIM(value))
        FROM dbo.SplitString(@SearchText, ',')
        WHERE LTRIM(RTRIM(value)) <> '';
    END

    -- Lấy dữ liệu kết quả
    SELECT
        OrganID,
        OrganAdd,
        OrganName,
        FastTypeCode,
        InOrder
    FROM [DHTN-2025].edoc.InterOrganization
    WHERE 
        IsDeleted = 0
        AND (
            (@OrgType = '-1000' AND OrganID IN (SELECT Code FROM #SearchIds))
            OR
            (@OrgType <> '-1000' AND
                (
                    @SearchText IS NULL
                    OR (dbo.fRemoveDiacritics(FastTypeCode) IN (
                        SELECT dbo.fRemoveDiacritics(Code) FROM #SearchIds
                    ))
                )
                AND (@OrgType IS NULL OR OrgType IN (SELECT OrgType FROM #OrgTypeTable))
            )
        )
    ORDER BY 
        InOrder ASC;

    DROP TABLE #OrgTypeTable;
    DROP TABLE #SearchIds;
END
